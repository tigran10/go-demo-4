pipeline {
    agent {
        kubernetes {
            label "builder-pod-${UUID.randomUUID().toString()}"
            cloud "go-demo-4-build"
            defaultContainer 'jnlp'
            yamlFile "DeploymentKubernetesPod.yaml"
        }
    }

    environment {
        cmAddr = "cm.acme.com"
    }

    options {
        buildDiscarder logRotator(numToKeepStr: '5')
        disableConcurrentBuilds()
    }

    stages {

        stage('Select Project') {
            steps {
                showProject()
            }
        }

        stage("Select Version") {
            steps {
                container("jq") {
                    showVersion()
                }
            }
        }


        stage("Select Environment") {
            steps {
                showEnvironment()
            }

        }

        stage("Deploy") {
            steps {
                container("helm") {
                    addChartmuseum()
                    installChart()
                }

                container("kubectl") {
                    ciK8sRollout(env.project, env.environment)
                }
            }
        }

        stage("Test") {
            steps {
                container("golang") {
                    k8sProdTestGolang(getAddr())
                }
            }
        }
    }

}


def addChartmuseum() {
    withCredentials([usernamePassword(
            credentialsId: "chartmuseum",
            usernameVariable: "USER", passwordVariable: "PASS")]) {

        sh """helm repo add chartmuseum \
                                http://${env.cmAddr} \
                                --username ${USER} \
                                --password ${PASS}
                           """
    }
}

def installChart() {
    sh """helm upgrade -i ${env.project} chartmuseum/${env.project} \
                            --version ${env.projectVersion} \
                            --tiller-namespace ${env.environment} \
                            --namespace ${env.environment} \
                            --set ingress.host=${getAddr()} """
}

def getAddr() {
    getEnvByName(env.project, env.environment).addr
}

def getProjectByName(n) {
    def dpl = readYaml file: 'platform_deployment.yml'
    for (s in dpl.services) {
        if (s.name == n) {
            return s
        }
    }
}

def getEnvByName(project, envName) {
    for (e in getProjectByName(project).envs) {
        if (e.name == envName) {
            return e
        }
    }
}

def showProject() {
    def deployment = readYaml file: "platform_deployment.yml"
    def projectChoices = ''
    deployment.services.each {
        projectChoices += it.name + '\n'
    }

    env.project = input(id: 'projectInput',
            message: 'Choose properties file',
            parameters: [[$class     : 'ChoiceParameterDefinition',
                          choices    : projectChoices,
                          description: 'What service to deploy?',
                          name       : 'project']])
}

def showVersion() {
    def projectVersions = sh(
    script: """  curl  https://api.github.com/repos/vfarcic/${env.project}/releases | jq -r ".[].tag_name" """,
    returnStdout: true).trim()

    env.projectVersion = input(id: 'versionInput',
            message: 'Choose version',
            parameters: [[$class     : 'ChoiceParameterDefinition',
                          choices    : projectVersions,
                          description: 'What version to deploy?',
                          name       : 'version']])
}

def showEnvironment() {
    def envs = getProjectByName(env.project).envs.name
    env.environment = input(id: 'envInput',
            message: 'Choose environment',
            parameters: [[$class     : 'ChoiceParameterDefinition',
                          choices    : envs,
                          description: 'Where to deploy?',
                          name       : 'env']])
}
